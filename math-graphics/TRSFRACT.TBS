10 REM FRACTALS FOR THE ATARI (ADAPTED FROM TRSBASIC)
15 REM SCREEN PIXEL SIZES
20 *F :CF=3.14159/180:MX=620:MY=240
30 INPUT "NUMBER OF SIDES ";S
35 REM BEGINNING X AND Y (SCALED TO SCREEN SIZE)
40 CX=MX*0.7:CY=MY*0.8:X2=CX:Y2=MY+1-CY
50 FRSETLEN=300:DIM I$(1),A$(FRSETLEN),C$(FRSETLEN),A1$(FRSETLEN),B$(FRSETLEN),B1$(FRSETLEN),Y$(1),INP$(1)
60 INPUT "INVERSE ";I$
70 REM ANGLES TO TURN RIGHT AND LEFT
80 R=-360/S:L=R+180
90 IF I$="Y" OR I$="y":C$="A":W=R:R=-L:L=-W:ELSE :C$="R":ENDIF 
100 A$=C$
110 IF I$<>"Y" AND I$<>"y" THEN 140
120 A1$="R":S1=S-2:FOR N=1 TO S1:A1$(LEN(A1$)+1)="L":NEXT N:A1$(LEN(A1$)+1)="R"
130 GOTO 160
140 A1$="L":S1=S-2:FOR N=1 TO S1:A1$(LEN(A1$)+1)="R":NEXT N
150 A1$(LEN(A1$)+1)="L"
160 INPUT "NUMBER OF LEVELS ";LE:IF LE=1 THEN 230
170 REM EXPAND STRING FOR MULTIPLE LEVELS
190 FOR N=2 TO LE:LN=LEN(A$)
200   FOR N1=1 TO LN:B$(LEN(B$)+1)=A$(N1,N1):B$(LEN(B$)+1)=A1$:NEXT N1
210   A$=B$:B$="":NEXT N
230 REM SCALE SIZE OF DIAGRAM TO FIT ON SCREEN
240 LL=MX*1.25/(S*3^LE)
250 REM CLEAR GRAPHICS SCREEN AND SET TO STARTING X,Y
260 GRAPHICS 8:COLOR 1:SETCOLOR 2,0,0
263 EXEC DISPPARMS
270 FOR Z=1 TO S
280   FOR N=1 TO LEN(A$)
290     B1$=A$(N,N):B1$(LEN(B1$)+1)=A1$
300     FOR N1=1 TO LEN(B1$):B$=B1$(N1,N1)
310       IF B$="A" THEN A=A+W:GOTO 330
320       IF B$="L":A=A+L:ELSE :A=A+R:ENDIF 
330       A1=A*CF
340       X=X+LL*COS(A1):Y=Y+LL*SIN(A1)
350       XP=INT(X):YP=INT(Y):X1=XP+CX:Y1=241-(YP/2+CY)
355       EXEC DRAW
360       X2=X1:Y2=Y1
370     NEXT N1:NEXT N:NEXT Z
380 REM END OF FIGURE
400 A$=""
450 ? :? "Another diagram?";:Y$=""
460 Y$=INKEY$:IF Y$="" THEN 460
470 IF Y$="Y" OR Y$="y" THEN GRAPHICS 0:RUN 
480 GRAPHICS 0:END 
500 PROC DRAW
505   IF X2>0 AND Y2>0
510     SX=X2:SY=Y2:EXEC SCALECOORDS:PLOT SCX,SCY
530     SX=X1:SY=Y1:EXEC SCALECOORDS:DRAWTO SCX,SCY
540   ENDIF 
550 ENDPROC 
600 PROC SCALECOORDS
610   SCX=SX*0.51451613:SCY=SY*0.6625
620 ENDPROC 
670 PROC DISPPARMS
680   TEXT 0,0,"Sides:":TEXT 56,0,STR$(S)
690   TEXT 0,8,"Inverse:":TEXT 72,8,I$
700   TEXT 0,16,"# of levels:":TEXT 104,16,STR$(LE)
710   TEXT 0,32,"Fractal:"
720   FRLEN=LEN(A$)
730   GRLINE=40
740   FOR I=1 TO FRLEN STEP 10
750     IF I+9>FRLEN:TEXT 0,GRLINE,A$(I):ELSE :TEXT 0,GRLINE,A$(I,I+9):GRLINE=GRLINE+8:ENDIF 
760     IF GRLINE>159 THEN EXIT 
770   NEXT I
780 ENDPROC 
