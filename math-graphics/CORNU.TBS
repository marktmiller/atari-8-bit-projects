10 SIZE=720
20 GRAPHICS 8+16:COLOR 1:SETCOLOR 2,0,0
30 FOR X=-1 TO 1 STEP 0.1
40   X1C=X*SIZE:Y1C=-SIZE:X2C=X*SIZE:Y2C=SIZE:EXEC BBCLINE
50 NEXT X
60 FOR Y=-1 TO 1 STEP 0.1
70   X1C=-SIZE:Y1C=Y*SIZE:X2C=SIZE:Y2C=Y*SIZE:EXEC BBCLINE
80 NEXT Y
90 V1=1E-03
100 CIRX=0:CIRY=0:CIRRAD=1:EXEC BBCCIR
110 FOR J=1 TO 2
120   K=0:V=0
130   XC=0:YC=0:EXEC BBCPLOT
140   REPEAT 
150     D=V/100:X2=0:X3=0:Y2=0:Y3=0:X1=1:Y1=0:Z=D
160     PI=3.14159
170     FOR I=1 TO 99 STEP 2
180       X2=X2+COS(PI*Z*Z/2):Y2=Y2+SIN(PI*Z*Z/2)
190       Z=Z+2*D
200     NEXT I
210     Z=2*D
220     FOR I=2 TO 98 STEP 2
230       X3=X3+COS(PI*Z*Z/2):Y3=Y3+SIN(PI*Z*Z/2)
240       Z=Z+2*D
250     NEXT I
260     X4=COS(PI*V*V/2):Y4=SIN(PI*V*V/2)
270     X=(D/3)*(X1+X4+4*X2+2*X3):Y=(D/3)*(Y1+Y4+4*Y2+2*Y3)
280     XC=X*SIZE:YC=Y*SIZE:EXEC BBCDRAW
290     V=V+V1:K=K-1
300     IF K<-100
310       CIRX=X*SIZE:CIRY=Y*SIZE:CIRRAD=1:EXEC BBCCIR
320       K=0
330     ENDIF 
340   UNTIL ABS(V)>=5
350   V1=-1E-03
360 NEXT J
370 TEXT 319-32,183,"DONE"
380 GOTO 380
390 PROC CNVCOORD
400   LXC=256+192*(XC-720)/1440:LYC=191-191*(YC+720)/1440
410 ENDPROC 
420 PROC BBCPLOT
430   EXEC CNVCOORD
440   PLOT LXC,LYC
450 ENDPROC 
460 PROC BBCDRAW
470   EXEC CNVCOORD
480   DRAWTO LXC,LYC
490 ENDPROC 
500 PROC BBCLINE
510   XC=X1C:YC=Y1C:EXEC BBCPLOT
520   XC=X2C:YC=Y2C:EXEC BBCDRAW
530 ENDPROC 
540 PROC BBCCIR
550   XC=CIRX:YC=CIRY:EXEC CNVCOORD
560   CIRCLE LXC,LYC,CIRRAD
570 ENDPROC 
