10 SMAXX=319:SMAXY=191:ORMAXX=640:ORMAXY=480
20 REM WIREFRAME PYRAMID WITH ROTATION
30 ? "Press any key to quit out.":PAUSE 120
40 EXEC INIT
50 REPEAT 
60   EXEC FLIP
70   REM CALCULATE ROTATION
80   FOR I=0 TO 4
90     X=PYRAMID(I,0):Y=PYRAMID(I,1):Z=PYRAMID(I,2)
100     REM ROTATE AROUND X-AXIS
110     Y1=Y*COS(ANGLEX)-Z*SIN(ANGLEX)
120     Z1=Y*SIN(ANGLEX)+Z*COS(ANGLEX)
130     REM ROTATE AROUND Y-AXIS
140     X1=X*COS(ANGLEY)+Z1*SIN(ANGLEY)
150     Z2=-X*SIN(ANGLEY)+Z1*COS(ANGLEY)
160     REM STORE ROTATED COORDINATES
170     ROTATED(I,0)=X1:ROTATED(I,1)=Y1:ROTATED(I,2)=Z2
180   NEXT I
190   REM DRAW PYRAMID EDGES (PROJECT 3D TO 2D)
200   FOR I=1 TO 4
210     SX=ROTATED(0,0):SY=ROTATED(0,1):EXEC SCALECOORDS
220     EXEC PLCOORD
230     SX=ROTATED(I,0):SY=ROTATED(I,1):EXEC SCALECOORDS
240     EXEC DRCOORD
250   NEXT I
260   SX=ROTATED(1,0):SY=ROTATED(1,1):EXEC SCALECOORDS
270   EXEC PLCOORD
280   SX=ROTATED(2,0):SY=ROTATED(2,1):EXEC SCALECOORDS
290   EXEC DRCOORD
300   SX=ROTATED(3,0):SY=ROTATED(3,1):EXEC SCALECOORDS
310   EXEC DRCOORD
320   SX=ROTATED(4,0):SY=ROTATED(4,1):EXEC SCALECOORDS
330   EXEC DRCOORD
340   SX=ROTATED(1,0):SY=ROTATED(1,1):EXEC SCALECOORDS
350   EXEC DRCOORD
360   REM UPDATE ANGLES
370   ANGLEX=ANGLEX+0.05
380   ANGLEY=ANGLEY+0.03
390 UNTIL INKEY$<>""
400 POKE 106,P106:GRAPHICS 0:END 
410 PROC INIT
420   RESTORE 1500:FOR I=$6000 TO $6000+96:READ A:POKE I,A:NEXT I
430   REM SET UP SCREEN 1
440   GRAPHICS 8+16
450   DL1=DPEEK(560):P106=PEEK(106)
460   SCR1=DPEEK(88):SCRBASE=PEEK(89)
470   REM SET UP SCREEN 2
480   REM MOVING RAMTOP LOWER GETS OS TO SET UP SCREEN MEMORY LOWER
490   REM MOVING RAMTOP BACK 32 PAGES (8K)
500   POKE 106,P106-32
510   GRAPHICS 8+16:SETCOLOR %2,%0,%0:COLOR %1
520   DL2=DPEEK(560)
530   SCR2=DPEEK(88)
540   DIM PYRAMID(5,3),ROTATED(5,3)
550   REM DEFINE PYRAMID VERTICES
560   PYRAMID(0,0)=0:REM X
570   PYRAMID(0,1)=100:REM Y
580   PYRAMID(0,2)=0:REM Z (TOP VERTEX)
590   PYRAMID(1,0)=-50:REM X
600   PYRAMID(1,1)=-50:REM Y
610   PYRAMID(1,2)=50:REM Z
620   PYRAMID(2,0)=50
630   PYRAMID(2,1)=-50
640   PYRAMID(2,2)=50
650   PYRAMID(3,0)=50
660   PYRAMID(3,1)=-50
670   PYRAMID(3,2)=-50
680   PYRAMID(4,0)=-50
690   PYRAMID(4,1)=-50
700   PYRAMID(4,2)=-50
710   REM DISPLAY SCREEN 1
720   SCR=%1
730 ENDPROC 
740 PROC FLIP
750   IF SCR=%1
760     REM DISPLAY SCREEN 1, DRAW ON SCREEN 2
770     DPOKE 560,DL1:POKE 89,SCRBASE-32
780     REM CLEAR SCREEN 2
790     X=USR($6000,SCR2,0,320/8*192)
800     REM SET UP FLIP TO SCREEN 2 ON NEXT CALL
810     SCR=%2
820   ELSE 
830     REM DISPLAY SCREEN 2, DRAW ON SCREEN 1
840     DPOKE 560,DL2:POKE 89,SCRBASE
850     REM CLEAR SCREEN 1
860     X=USR($6000,SCR1,0,320/8*192)
870     REM SET UP FLIP TO SCREEN 1 ON NEXT CALL
880     SCR=%1
890   ENDIF 
900 ENDPROC 
910 PROC SCALECOORDS
920   SCX=SMAXX+SMAXX*(SX-ORMAXX)/640
930   SCY=SMAXY-SMAXY*(SY+ORMAXY)/480
940 ENDPROC 
950 PROC PLCOORD
960   PLOT 159+SCX,96+SCY
970 ENDPROC 
980 PROC DRCOORD
990   DRAWTO 159+SCX,96+SCY
1000 ENDPROC 
1500 DATA 104,104,133,3,104,133,2,104,104,141,96,96,104,141,95,96,104,56,233,1,141,94,96,173,95,96,233,0,141
1510 DATA 95,96,165,2,24,109,94,96,141,94,96,165,3,109,95,96,141,95,96,160,0,173,96,96,145,2,200,208,251,230
1520 DATA 3,165,3,205,95,96,208,239,173,94,96,197,2,48,19,56,229,2,141,94,96,173,96,96,145,2,204,94,96,240
1530 DATA 3,200,208,246,96,0,0,0
