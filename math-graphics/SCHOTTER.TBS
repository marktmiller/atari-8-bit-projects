10 R=5*1.4142:PIHALB=3.14159*0.5:PI4T=PIHALB*0.5:I=0
20 GRAPHICS 8+16:COLOR 1
30 QUER=10:HOCH=10:XMAL=22:YMAL=12:EXEC SERIE
40 GOTO 40
50 PROC QUAD
60   JE1=5*I/264:JA1=-JE1:JE2=PI4T*(1+I/264):JA2=PI4T*(1-I/264)
70   EXEC J1RND:EXEC J2RND
80   P1=P+5+J1:Q1=Q+5+J1:PSI=J2
90   X=P1+R*COS(PSI):Y=Q1+R*SIN(PSI):EXEC LEER
100   FOR S=1 TO 4
110     PSI=PSI+PIHALB
120     X=P1+R*COS(PSI):Y=Q1+R*SIN(PSI):EXEC LINE
130   NEXT S:I=I+1
140 ENDPROC 
150 PROC LEER
160   XADJ=(Y+125)*1.276:YADJ=(125+X)*0.744
170   PLOT XADJ,YADJ
180 ENDPROC 
190 PROC SERIE
200   P=-QUER*XMAL*0.5:Q=-HOCH*YMAL*0.5:YANF=Q
210   FOR COUNTX=1 TO XMAL
220     Q=YANF
230     FOR COUNTY=1 TO YMAL
240       EXEC QUAD:Q=Q+HOCH
250     NEXT COUNTY:P=P+QUER
260   NEXT COUNTX
270 ENDPROC 
280 PROC J1RND
290   RANGE=ABS(JA1-JE1):J1=RND(0)*RANGE+JE1
300 ENDPROC 
310 PROC J2RND
320   RANGE=ABS(JA2-JE2):J2=RND(0)*RANGE+JE2
330 ENDPROC 
340 PROC LINE
350   XADJ=(Y+125)*1.276:YADJ=(125+X)*0.744
360   DRAWTO XADJ,YADJ
370 ENDPROC 
