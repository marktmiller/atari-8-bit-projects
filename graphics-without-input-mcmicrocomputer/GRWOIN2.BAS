80 DIM NUM$(2)
90 SMAXX=319:SMAXY=191:GWMINX=-10:GWMINY=-6:GWMAXX=10:GWMAXY=6
100 REM samples of surfaces in space
110 S=2:DIM I$(1)
120 TH=0.3:S1=SIN(TH):C1=COS(TH):PH=0.4:S2=SIN(PH):C2=COS(PH)
130 REM Main loop
140 FOR Q=1 TO 10:GOSUB 300
150   REM meridians
160   FOR Y=-20 TO 20 STEP S:FL=0:FOR X=-20 TO 20 STEP S
170       ON Q GOSUB 340,350,360,370,380,390,400,410,420,430
180       GOSUB 280:IF FL=0 THEN FL=1:EXEC SCALEPLOT:REM (SX,SY)
190       EXEC SCALEDRAWTO:NEXT X:NEXT Y:REM (SX,SY)
200   REM parallels
210   FOR X=-20 TO 20 STEP S:FL=0:FOR Y=-20 TO 20 STEP S
220       ON Q GOSUB 340,350,360,370,380,390,400,410,420,430
230       GOSUB 280:IF FL=0 THEN FL=1:EXEC SCALEPLOT:REM (SX,SY)
240       EXEC SCALEDRAWTO:NEXT Y:NEXT X:NEXT Q:REM (SX,SY)
250 I$=INKEY$:IF I$="" THEN 250
260 END 
270 REM routine
280 REM Transform from 3D to 2D
290 SX=X1*C1-Y1*C2:SY=X1*S1+Y1*S2+Z1:RETURN 
300 REM screen cleaning and reference tracking
310 I$=INKEY$:IF I$="" THEN 310
320 GRAPHICS 8+16:COLOR 1:SETCOLOR 2,3,5
322 IF Q<10:TEXT 2,2,CHR$(48+Q+128):ELSE :TEXT 2,2,"��":ENDIF 
325 RETURN 
330 REM functions
340 X1=X/5:Y1=Y/5:Z1=(1-COS(X1))*(1-COS(Y1))-3:RETURN 
350 X1=X/6:Y1=Y/6:Z1=0.4*COS(X1*X1+Y1*Y1):RETURN 
360 X1=X/4:Y1=Y/4:Z1=EXP(0.02*(X1*X1+Y1*Y1))-3:RETURN 
370 X1=X/4:Y1=Y/4:Z1=0.02*((X1*Y1*Y1)-(Y1*X1*X1)):RETURN 
380 X1=X/4:Y1=Y/4:Z1=0.02*((X1*Y1*Y1)+(Y1*X1*X1)):RETURN 
390 X1=X/4:Y1=Y/4:Z1=3*SIN(X1/2)*SIN(Y1/3):RETURN 
400 X1=X/4:Y1=Y/4:K=(1-X1*X1/5-Y1*Y1/7):Z1=SQR(2*K*(SGN(K)+1)):RETURN 
410 X1=X/6:Y1=Y/6:Z1=0.02*SQR(X*X*Y*Y/2)-3:RETURN 
420 X1=X/5:Y1=Y/5:Z1=(1-SIN(1.8*X1))*(1-COS(1.2*Y1))-2:RETURN 
430 X1=X/5:Y1=Y/5:K=X1*X1+Y1*Y1:Z1=0.3*(1+COS(K))*(SGN(9.87-K)+1):RETURN 
440 REM GW-Basic graphics emulation routines
450 REM drawing logic
460 PROC SCALEPLOT
470   EXEC SCALECOORDS:CLIPREGSTART=0
480   STARTX=SCX:STARTY=SCY:DESTX=0:DESTY=0
490   IF SCX>=0 AND SCX<=SMAXX AND SCY>=0 AND SCY<=SMAXY
500     PLOT SCX,SCY:CURSSET=1
510   ELSE 
520     EXEC FINDCLIPREGSTART:CURSSET=0
530   ENDIF 
540 ENDPROC 
550 PROC SCALEDRAWTO
560   EXEC SCALECOORDS
570   DESTX=SCX:DESTY=SCY
580   IF CURSSET=1 AND SCX>=0 AND SCX<=SMAXX AND SCY>=0 AND SCY<=SMAXY
590     REM If line within bounds of scrn.
600     STARTX=SCX:STARTY=SCY:DRAWTO SCX,SCY
610   ELSE 
620     EXEC FINDCLIPREGDEST
630     IF CLIPREGSTART&1=1 AND CLIPREGDEST=0
640       EXEC CLIPLEFT:REM XC=0
650       REM If line starts outside lft. side of scrn., but ends inside scrn.
660       EXEC CLIPPLOT:GO# EXITDR
670     ELSE 
680       IF CLIPREGSTART=0 AND CLIPREGDEST&1=1
690         EXEC CLIPLEFT:REM XC=0
700         REM If line starts inside scrn., but ends outside lft. side of scrn.
710         EXEC CLIPDRAWTO:GO# EXITDR
720       ENDIF 
730     ENDIF :REM CLIPREGSTART&1=1 AND CLIPREGDEST=0
740     IF CLIPREGSTART&2=2 AND CLIPREGDEST=0
750       EXEC CLIPTOP:REM YC=0
760       REM If line starts outside top of scrn. but ends inside scrn.
770       EXEC CLIPPLOT:GO# EXITDR
780     ELSE 
790       IF CLIPREGSTART=0 AND CLIPREGDEST&2=2
800         EXEC CLIPTOP:REM YC=0
810         REM If line starts inside screen but ends outside top of scrn.
820         EXEC CLIPDRAWTO:GO# EXITDR
830       ENDIF 
840     ENDIF :REM CLIPREGSTART&2=2 AND CLIPREGDEST=0
850     IF CLIPREGSTART&4=4 AND CLIPREGDEST=0
860       EXEC CLIPRIGHT:REM XC=SMAXX
870       REM If line starts outside rt. side of scrn. but ends inside scrn.
880       EXEC CLIPPLOT:GO# EXITDR
890     ELSE 
900       IF CLIPREGSTART=0 AND CLIPREGDEST&4=4
910         EXEC CLIPRIGHT:REM XC=SMAXX
920         REM If line starts inside scrn. but ends outside rt. side of scrn.
930         EXEC CLIPDRAWTO:GO# EXITDR
940       ENDIF 
950     ENDIF 
960     REM CLIPREGSTART&4=4 AND CLIPREGDEST=0
970     IF CLIPREGSTART&8=8 AND CLIPREGDEST=0
980       EXEC CLIPBOTTOM:REM YC=SMAXY
990       REM If line starts beyond bot. of scrn. but ends inside scrn.
1000       EXEC CLIPPLOT:GO# EXITDR
1010     ELSE 
1020       IF CLIPREGSTART=0 AND CLIPREGDEST&8=8
1030         EXEC CLIPBOTTOM:REM YC=SMAXY
1040         REM If line starts inside scrn. but ends below bot. of scrn.
1050         EXEC CLIPDRAWTO:GO# EXITDR
1060       ENDIF 
1070     ENDIF 
1080     REM CLIPREGSTART&8=8 AND CLIPREGDEST=0
1090     REM If SCX and SCY are outside the scrn., update STARTX and STARTY
1100     STARTX=SCX:STARTY=SCY
1110   ENDIF :REM IF CURSSET=1 AND SCX>=0 AND SCX<=SMAXX AND SCY>=0 AND SCY<=SMAXY
1120   # EXITDR:ENDPROC 
1130 PROC SCALECOORDS
1140   SCX=SMAXX+SMAXX*(SX-GWMAXX)/(GWMAXX-GWMINX):SCY=SMAXY-SMAXY*(SY+GWMAXY)/(GWMAXY-GWMINY)
1150 ENDPROC 
1160 PROC CLIPLEFT
1170   YC=STARTY-STARTX/(DESTX-STARTX)*(DESTY-STARTY)
1180   XC=0
1190 ENDPROC 
1200 PROC CLIPTOP
1210   XC=STARTX-STARTY/(DESTY-STARTY)*(DESTX-STARTX)
1220   YC=0
1230 ENDPROC 
1240 PROC CLIPRIGHT
1250   YC=(SMAXX-STARTX)/(DESTX-STARTX)*(DESTY-STARTY)+STARTY
1260   XC=SMAXX
1270 ENDPROC 
1280 PROC CLIPBOTTOM
1290   XC=(SMAXY-STARTY)/(DESTY-STARTY)*(DESTX-STARTX)+STARTX
1300   YC=SMAXY
1310 ENDPROC 
1320 PROC FINDCLIPREGSTART
1330   CLIPREGSTART=0
1340   IF STARTX<0 THEN CLIPREGSTART=CLIPREGSTART!1:REM Lft. side
1350   IF STARTY<0 THEN CLIPREGSTART=CLIPREGSTART!2:REM Top
1360   IF STARTX>SMAXX THEN CLIPREGSTART=CLIPREGSTART!4:REM Rt. side
1370   IF STARTY>SMAXY THEN CLIPREGSTART=CLIPREGSTART!8:REM Bottom
1380 ENDPROC 
1390 PROC FINDCLIPREGDEST
1400   CLIPREGDEST=0
1410   IF DESTX<0 THEN CLIPREGDEST=CLIPREGDEST!1:REM Lft. side
1420   IF DESTY<0 THEN CLIPREGDEST=CLIPREGDEST!2:REM Top
1430   IF DESTX>SMAXX THEN CLIPREGDEST=CLIPREGDEST!4:REM Rt. side
1440   IF DESTY>SMAXY THEN CLIPREGDEST=CLIPREGDEST!8:REM Bottom
1450 ENDPROC 
1460 PROC CLIPPLOT
1470   STARTX=XC:STARTY=YC:PLOT XC,YC:CURSSET=1
1480 ENDPROC 
1490 PROC CLIPDRAWTO
1500   DRAWTO XC,YC:CURSSET=0:STARTX=SCX:STARTY=SCY
1510   CLIPREGSTART=CLIPREGDEST
1520 ENDPROC
