80 GR8MAXX=319:GR8MAXY=159:PCSCRDIMX=320:PCSCRDIMY=200
90 DIM I$(1),S$(1):SMAXX=PCSCRDIMX*(GR8MAXY+1)/PCSCRDIMY:SMAXY=GR8MAXY
100 REM Curves on the Epicycloid and Hypercycloid plane
110 GRAPHICS 8:COLOR 1:SETCOLOR 2,3,5:P=3.1415926
120 GWMAXX=20:GWMAXY=12:GWMINX=-20:GWMINY=-12
130 A=4:B=1.8:C=(A+B)/B:D=(A-B)/B
140 Q=0:GOSUB 230:EXEC SCALEPLOT
150 I$=INKEY$:IF I$<>"" THEN 170
160 Q=Q+0.1:GOSUB 230:EXEC SCALEDRAWTO:GOTO 150
170 SETCOLOR 2,0,0:INPUT "Press RETURN to Continue",S$
180 GRAPHICS 8:COLOR 1:SETCOLOR 2,3,5:P=3.1415926
190 GWMAXX=8:GWMAXY=6:GWMINX=-8:GWMINY=-6
200 Q=0:GOSUB 260:EXEC SCALEPLOT
210 I$=INKEY$:IF I$<>"" THEN SETCOLOR 2,0,0:END 
220 Q=Q+0.1:GOSUB 260:EXEC SCALEDRAWTO:GOTO 210
230 REM first function
240 X=1.5*(A+B)*COS(Q)-B*COS(C*Q):Y=(A+B)*SIN(Q)-B*SIN(C*Q):RETURN 
250 REM second function
260 X=1.5*(A-B)*COS(Q)+B*COS(D*Q):Y=(A-B)*SIN(Q)-B*SIN(D*Q):RETURN 
270 REM GW-Basic graphics emulation routines
280 REM drawing logic
290 PROC SCALEPLOT
300   EXEC SCALECOORDS
310   STARTX=SCX:STARTY=SCY:DESTX=0:DESTY=0
320   PLOT SCX,SCY
330 ENDPROC 
340 PROC SCALEDRAWTO
350   EXEC SCALECOORDS
360   DESTX=SCX:DESTY=SCY
370   STARTX=SCX:STARTY=SCY:DRAWTO SCX,SCY
380 ENDPROC 
390 PROC SCALECOORDS
400   SCX=(GR8MAXX-SMAXX)/2+SMAXX+SMAXX*(X-GWMAXX)/(GWMAXX-GWMINX)
410   SCY=SMAXY-SMAXY*(Y+GWMAXY)/(GWMAXY-GWMINY)
420 ENDPROC
