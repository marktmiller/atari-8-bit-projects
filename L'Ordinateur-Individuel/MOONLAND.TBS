10 REM From L'Ordinateur Individuel, Dec. 1983, by Francis Reignat
20 REM Atari port by Mark Miller, 2025
50 GRAPHICS 0:SETCOLOR %2,%0,%0:? "            LUNAR LANDING":? :? :? :? :? :? 
80 ? " USE JOYSTICK:":? :? " LEFT - RIGHT"
90 ? " BUTTON: RETRO ROCKETS":? :? " PRESS A KEY"
95 IF INKEY$="" THEN 95
100 DIM YY(160)
110 Y=93:FACTOR=10:EXEC RD:H=RN:U=%0:V=%0
120 REM calculate landscape
130 GRAPHICS 7+16:MODE=%2:SETCOLOR %2,%0,%0:COLOR %1:SETCOLOR %0,8,13
150 REM the scene before the base (calculation)
160 YY(%0)=RND*20
170 FOR X=%0 TO 58
180   # G:FACTOR=%2:EXEC RD:YY(X+%1)=YY(X)+RN
190   IF YY(X+%1)<%2 THEN GO# G
200 NEXT X
210 REM draw the base (calculation)
220 FOR X=59 TO 67:YY(X+%1)=YY(59):NEXT X
230 REM draw the scene after the base
240 FOR X=68 TO 159
250   # H:FACTOR=%2:EXEC RD:YY(X+%1)=YY(X)+RN
260   IF YY(X+%1)<%2 THEN GO# H
270 NEXT X
280 REM actual drawing of the whole scene
290 FOR X=%0 TO 159:SX=X:SY=%0:EXEC PL:PAUSE %1:SX=X:SY=YY(X):EXEC DR:NEXT X:X=93
300 REM drawing of the frame and highlighting the base
310 COLOR %0:SX=%0:SY=50:EXEC PL:SX=%0:SY=%0:EXEC DR:SX=159:SY=%0:EXEC DR:SX=159:SY=50:EXEC DR
320 SX=60:SY=YY(59)-%1:EXEC PL:SX=67:SY=YY(59)-%1:EXEC DR
330 REM main sequence of prog.
340 REM erase module
350 # D:COLOR %0:EXEC DRMODULE:A=%1
360 REM respond to player inputs
370 IF STRIG(%0)=%0 THEN V=V+%2:A=%0:SOUND %2,6,%0,%1
380 IF STICK(%0)=7 THEN H=H+%1:SOUND %1,%0,%0,%1
390 IF STICK(%0)=11 THEN H=H-%1:SOUND %1,%0,%0,%1
392 IF STRIG(%0)=%1 THEN SOUND %2,%0,%0,%0
394 IF STICK(%0)<>7 AND STICK(%0)<>11 THEN SOUND %1,%0,%0,%0
400 REM calculate module position
410 V=V-%1:Y=Y+V:X=X+H
420 IF X<%2 THEN X=%2
430 IF X>157 THEN X=157
435 IF MODE=%0 AND X>77 THEN X=77
440 COLOR %1:EXEC DRMODULE
450 REM delay
460 FOR T=%0 TO 8:PAUSE %1:NEXT T
470 REM crash or landing?
480 IF MODE=%0
483   IF X>39 AND X<49 AND Y-%3<YY(X+20) THEN GO# R
485 ENDIF 
490 IF MODE=%2
492   IF Y-%2<YY(X-%2) OR Y-%2<YY(X+%2) THEN GO# E
494 ENDIF 
495 IF MODE=%0
496   IF Y-%2<YY(X+20-%2) OR Y-%2<YY(X+20+%2) THEN GO# E
498 ENDIF 
500 REM enlargement?
510 IF U=%0 AND Y-YY(64)<25 AND X>58 AND X<67 THEN EXEC ENLARGE
520 GO# D
530 REM end of the main sequence
540 REM draw explosion & comments
550 # E:SOUND %1,%0,%0,%0:SOUND %2,%0,%0,%0
552 IF V>-4
554   IF MODE=%0:BDIST=43:ELSE :BDIST=63:ENDIF 
558   GRAPHICS %0:SETCOLOR %2,%0,%0:? :? :? :? " AFTER THIS MAKESHIFT "
560   ? " MOON LANDING, ";INT(ABS(BDIST-X)*10)/10;" KM FROM THE BASE,"
570   ? "ALL YOU HAVE TO DO IS WALK...":GO# I
573 ENDIF 
575 SOUND %0,57,%0,12
580 FOR T=%0 TO 30:SX=X:SY=YY(T):EXEC PL:FACTOR=40:EXEC RD:SX=RN+X:SY=RND*50+YY(T):EXEC DR
590 NEXT T:SOUND %0,%0,%0,%0
600 FOR T=%0 TO 50:PAUSE %1:NEXT T
605 GRAPHICS %0:SETCOLOR %2,%0,%0:? :? :? :? :? :? 
610 ? :? :? :? " AFTER THIS MEMORABLE CRASH,"
620 ? "YOUR MODULE IS UNUSABLE!":? " THE BASE YOU SPARED"
630 ? "PAYS ITS RESPECTS..."
640 GO# I
650 REM draw the module
655 PROC DRMODULE
660   CKVAL=Y:EXEC CKYBND:IF PASS THEN SX=X-%1:SY=Y:EXEC PL:SX=X:EXEC PL:SX=X+%1:EXEC PL
670   CKVAL=Y+%1:EXEC CKYBND:IF PASS THEN SX=X-%1:SY=Y+%1:EXEC PL:SX=X:EXEC PL:SX=X+%1:EXEC PL
680   CKVAL=Y+%2:EXEC CKYBND:IF PASS THEN SX=X:SY=Y+%2:EXEC PL
685   CKVAL=Y-%1:EXEC CKYBND:IF PASS THEN SX=X-%2:SY=Y-%1:EXEC PL:SX=X+%2:SY=Y-%1:EXEC PL
690   IF A=%0
692     CKVAL=Y-%2:EXEC CKYBND:IF PASS THEN SX=X:SY=Y-%2:EXEC PL
694     CKVAL=Y-%3:EXEC CKYBND:IF PASS THEN SX=X:SY=Y-%3:EXEC PL
696     CKVAL=Y-4:EXEC CKYBND:IF PASS THEN SX=X:SY=Y-4:EXEC PL
698   ENDIF 
700 ENDPROC 
710 REM comments
720 # R:IF V>-4
722   SOUND %1,%0,%0,%0:SOUND %2,%0,%0,%0
725   GRAPHICS %0:SETCOLOR %2,%0,%0:? :? :? "          *************"
730   ? "          * WELL DONE *"
740   ? "          *************":? :? :? :? 
750   ? "     SUPERB LANDING!":? :? :? :? :GO# I
753 ENDIF 
755 SOUND %0,57,%0,12
760 FOR T=%0 TO 30:SX=X:SY=YY(X):EXEC PL:FACTOR=40:EXEC RD:SX=RN+X:SY=RND*50+YY(X):EXEC DR
770 NEXT T:SOUND %0,%0,%0,%0
780 FOR T=%0 TO 50:PAUSE %1:NEXT T
785 GRAPHICS %0:SETCOLOR %2,%0,%0
790 ? :? :? :? " YOU DESTROYED THE BASE..."
800 FOR Z=%0 TO 60:PAUSE 1:NEXT Z:? "NO ONE CAN"
810 ? " HELP YOU...":FOR Z=%0 TO 60:PAUSE 1:NEXT Z:? :? :? 
820 ? " SO, YOU WILL SUFFER AND DIE"
830 FOR Z=%0 TO 20:? "+";:NEXT Z:GO# I
840 REM enlarge the scene
845 PROC ENLARGE
850   GRAPHICS 5+16:MODE=%0:SETCOLOR %2,%0,%0:COLOR %1:SETCOLOR %0,8,13
851   SOUND %1,%0,%0,%0:SOUND %2,%0,%0,%0
853   FOR X=%0 TO 79
855     SX=X:SY=%0:EXEC PL:SX=X:SY=YY(X+20):EXEC DR
857   NEXT X
860   U=%1:X=X-40:COLOR %0:SX=40:SY=YY(59)-%1:EXEC PL:SX=47:SY=YY(59)-%1:EXEC DR
865 ENDPROC 
870 # I:FOR Z=%0 TO 300:PAUSE %1:NEXT Z:RUN 
1000 PROC RD
1010   IF RND*%2>%1:SIGN=-1:ELSE :SIGN=%1:ENDIF 
1020   RN=RND*FACTOR*SIGN
1030 ENDPROC 
1040 PROC PL
1050   IF SX<0 THEN SX=0
1060   IF SY<0 THEN SY=0
1070   PSAVEX=INT(SX)
1080   IF MODE=%2 THEN PLOT INT(SX),95-INT(SY):PSAVEY=95-INT(SY)
1090   IF MODE=%0 THEN PLOT INT(SX),47-INT(SY):PSAVEY=47-INT(SY)
1100 ENDPROC 
1110 PROC DR
1120   IF SX>=0
1130     IF MODE=%2
1140       IF SX<=159
1150         DRAWTO INT(SX),95-INT(SY)
1160       ELSE 
1170         EXEC CLIPRIGHT
1180       ENDIF 
1190     ELSE :REM MODE=0
1200       IF SX<=79 AND SY<=47
1210         DRAWTO INT(SX),47-INT(SY)
1220       ELSE 
1230         IF SX>79 AND SY>47
1240           EXEC CLIPTOPRIGHT
1250         ELSE 
1260           IF SX>79
1270             EXEC CLIPRIGHT
1280           ELSE 
1290             EXEC CLIPTOP
1300           ENDIF 
1310         ENDIF 
1320       ENDIF 
1330     ENDIF :REM IF MODE=2
1340   ELSE :REM SX<0
1350     IF MODE=%2
1360       EXEC CLIPLEFT
1370     ELSE 
1380       IF SY<=47
1390         EXEC CLIPLEFT
1400       ELSE 
1410         EXEC CLIPTOPLEFT
1420       ENDIF 
1430     ENDIF 
1440   ENDIF :REM IF SX>=0
1450 ENDPROC 
1460 REM Input: CKVAL
1470 REM Output: PASS
1480 PROC CKYBND
1490   PASS=0
1500   IF MODE=%2 AND CKVAL<95 OR MODE=%0 AND CKVAL<47 THEN PASS=1
1510 ENDPROC 
1520 PROC CLIPRIGHT
1530   IF SX<>PSAVEX AND SY<>PSAVEY
1540     IF MODE=%2
1550       MAXX=159:MAXY=95
1560     ELSE 
1570       MAXX=79:MAXY=47
1580     ENDIF 
1590     YC=(MAXX-PSAVEX)/(SX-PSAVEX)*(SY-PSAVEY)+PSAVEY
1600     DRAWTO MAXX,MAXY-INT(YC)
1610   ENDIF 
1620 ENDPROC 
1630 PROC CLIPLEFT
1640   IF SX<>PSAVEX AND SY<>PSAVEY
1650     IF MODE=%2:MAXY=95:ELSE :MAXY=47:ENDIF 
1660     YC=PSAVEY-PSAVEX/(SX-PSAVEX)*(SY-PSAVEY)
1670     DRAWTO 0,MAXY-INT(YC)
1680   ENDIF 
1690 ENDPROC 
1700 PROC CLIPTOP
1710   IF SX<>PSAVEX AND SY<>PSAVEY
1720     RI=(SY-PSAVEY)/10:RN=(SX-PSAVEX)/10:MAXY=47
1730     XC=SX-((SY-MAXY)/RI)*RN
1740     DRAWTO INT(XC),MAXY
1750   ENDIF 
1760 ENDPROC 
1770 REM Only used when MODE=0
1780 PROC CLIPTOPRIGHT
1790   IF SX<>PSAVEX AND SY<>PSAVEY
1800     MAXX=79:MAXY=47
1810     RI=(SY-PSAVEY)/10:RN=(SX-PSAVEX)/10
1820     REPEAT 
1830       SX=SX-RN:SY=SY-RI
1840     UNTIL SX<=MAXX AND SY<=MAXY
1850     DRAWTO INT(SX),MAXY-INT(SY)
1860   ENDIF 
1870 ENDPROC 
1880 REM Only used when MODE=0
1890 PROC CLIPTOPLEFT
1900   IF SX<>PSAVEX AND SY<>PSAVEY
1910     MAXY=47
1920     RI=(SY-PSAVEY)/10:RN=(PSAVEX+ABS(SX))/10
1930     IF RN=0 THEN RN=0.1
1940     ORSX=SX:ORSY=SY
1950     REPEAT 
1960       SX=SX+RN:SY=SY-RI
1970     UNTIL SX>=0 AND SY<=MAXY
1980     DRAWTO INT(SX),MAXY-INT(SY)
1990   ENDIF 
2000 ENDPROC 
