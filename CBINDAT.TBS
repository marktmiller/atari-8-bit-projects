10 DIM IFILE$(15),OFILE$(15),LSTART$(5),HX$(4)
20 ? "INPUT FILE ";:INPUT IFILE$
30 IF IFILE$(1,2)="D:" AND ASC(IFILE$(3,3))>=41 AND ASC(IFILE$(3,3))<=90
40   GOTO 130
50 ELSE 
60   IF IFILE$(1,1)="D" AND ASC(IFILE$(2,2))>=49 AND ASC(IFILE$(2,2))<=57 AND IFILE$(3,3)=":"
70     IF ASC(IFILE$(4,4))>=65 AND ASC(IFILE$(4,4))<=90:GOTO 130:ELSE :GOTO 110:ENDIF 
80   ELSE :GOTO 110
90   ENDIF 
100 ENDIF 
110 ? "FILENAME LIMITED TO 15 CHARACTERS WITH DRIVE SPEC. AND OPTIONAL 3-CHAR EXTENSION.";
120 ? "FIRST CHARACTER MUST BE A LETTER.":? :GOTO 20
130 ? "OUTPUT FILE ";:INPUT OFILE$
140 IF OFILE$(1,2)="D:" AND ASC(OFILE$(3,3))>=41 AND ASC(OFILE$(3,3))<=90
150   GOTO 240
160 ELSE 
170   IF OFILE$(1,1)="D" AND ASC(OFILE$(2,2))>=49 AND ASC(OFILE$(2,2))<=57 AND OFILE$(3,3)=":"
180     IF ASC(OFILE$(4,4))>=65 AND ASC(OFILE$(4,4))<=90:GOTO 240:ELSE :GOTO 220:ENDIF 
190   ELSE :GOTO 220
200   ENDIF 
210 ENDIF 
220 ? "FILENAME LIMITED TO 15 CHARACTERS OR LESS, WITH OPTIONAL 3-CHAR EXTENSION.";
230 ? "FIRST CHARACTER MUST BE A LETTER.":? :GOTO 130
240 ? "STARTING LINE # ";:INPUT LSTART$
250 IF ASC(LSTART$(1,1))>=48 AND ASC(LSTART$(1,1))<=57
260   LN=VAL(LSTART$)
270 ELSE 
280   ? "MUST BE A NUMBER.":? 
290   GOTO 240
300 ENDIF 
310 EXPT=0
320 OPEN #1,4,0,IFILE$:OPEN #2,8,0,OFILE$
330 TRAP 390
340 GET #1,BYTE:GET #1,BYTE:REM SKIP HEADER
350 GET #1,BYTE:GET #1,BYTE2:ADRSTART=BYTE+BYTE2*256
360 GET #1,BYTE:GET #1,BYTE2:ADREND=BYTE+BYTE2*256
370 MEMPTR=ADRSTART
380 GOTO 400
390 ? "CORRUPTED FILE. UNABLE TO COMPLETE.":CLOSE #1:CLOSE #2:END 
400 TRAP 780
410 BYTECNT=0
420 ? :? "SEGMENT: ";HEX$(ADRSTART);" - ";HEX$(ADREND):? 
430 DO 
440   I=10:LNDONE=0
450   IF MEMPTR<=ADREND THEN ? #2;STR$(LN);" DATA ";:? STR$(LN);" DATA ";
460   REPEAT 
470     IF MEMPTR<=ADREND
480       GET #1,BYTE:BYTECNT=BYTECNT+1
490     ELSE 
500       REM START NEW SEGMENT
510       GET #1,BYTE:GET #1,BYTE2:ADRSTART=BYTE+BYTE2*256
520       GET #1,BYTE:GET #1,BYTE2:ADREND=BYTE+BYTE2*256
530       LNDONE=1
540       ? :? :? "SEGMENT: ";HEX$(ADRSTART);" - ";HEX$(ADREND):? 
550       GOTO 670
560     ENDIF 
570     ITEMLEN=LEN(STR$(BYTE))
580     IF I=10
590       ? #2;BYTE;:? BYTE;:I=I+ITEMLEN
600     ELSE 
610       ? #2;",";BYTE;:? ",";BYTE;:I=I+ITEMLEN+1
620     ENDIF 
630     IF I+2*ITEMLEN+1>=110 OR MEMPTR=ADREND
640       ? #2:? :LNDONE=1
650     ENDIF 
660     MEMPTR=MEMPTR+1
670   UNTIL LNDONE=1
680   LN=LN+10
690 LOOP 
700 REM EXPT=1
710 REM ITEMLEN=LEN(STR$(BYTE))
720 REM IF LNDONE=0 AND I+ITEMLEN<110
730 REM ? #2;BYTE:? BYTE
740 REM CLOSE #1:CLOSE #2
750 REM ELSE
760 REM LN=LN+10:GOTO 460
770 REM ENDIF
780 CLOSE #1:CLOSE #2
790 ? :? BYTECNT;" BYTES":END 
