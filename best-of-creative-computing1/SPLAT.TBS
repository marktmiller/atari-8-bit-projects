10 REM From Best of Creative Computing, Vol. 1, p. 268
20 REM Original by John Yegge
40 DIM A(100),A1$(3),B1$(3),Z$(3)
45 ? "LOADING PARACHUTE DROP DATA"
50 TRAP 70:OPEN #1,4,0,"D:PARACH.UTE"
60 FOR I=0 TO 99:%GET #1;J:A(I)=J:NEXT I
70 CLOSE #1:TRAP 40000
95 ? :? "WELCOME TO 'SPLAT' == THE GAME THAT"
96 ? "SIMULATES A PARACHUTE JUMP. TRY TO"
97 ? "OPEN A CHUTE AT THE LAST POSSIBLE"
100 ? "MOMENT WITHOUT GOING SPLAT."
118 ? :? :V=0:A=0:N=0:M=0:D1=INT(9001*RND(0)+1000)
119 ? "SELECT YOUR OWN TERMINAL VELOCITY"
120 ? "(YES OR NO)";:INPUT A1$:IF A1$="NO":GOTO 128:ELSE :IF A1$="YES":GOTO 123:ENDIF :ENDIF 
121 ? "'YES' OR 'NO' PLEASE";:INPUT A1$:GOTO 120
123 ? "WHAT TERMINAL VELOCITY (MI/HR)";:INPUT V1
125 V1=V1*(5280/3600):V=V1+((V1*RND(0))/20)-((V1*RND(0))/20):GOTO 135
128 V1=INT(1000*RND(0))
130 ? "OK, TERMINAL VELOCITY=";V1;" MI/HR"
131 V1=V1*(5280/3600):V=V1+((V1*RND(0))/20)-((V1*RND(0))/20)
135 ? "WANT TO SELECT ACCELERATION DUE TO"
136 ? "GRAVITY (YES OR NO)";:INPUT B1$
140 IF B1$="NO":GOTO 150:ELSE :IF B1$="YES":GOTO 143:ENDIF :ENDIF 
141 ? "'YES' OR 'NO' PLEASE";:INPUT B1$:GOTO 140
143 ? "WHAT ACCELERATION (FT/SEC/SEC)";:INPUT A2
145 A=A2*((A2*RND(0))/20)-((A2*RND(0))/20):GOTO 205
150 ON INT(1+(10*RND(0))) GOTO 151,152,153,154,155,156,157,158,159,160
151 ? "FINE, YOU'RE ON MERCURY.":? "ACCELERATION=12.2 FT/SEC/SEC":GOTO 161
152 ? "ALRIGHT. YOU'RE ON VENUS.":? "ACCELERATION=28.3 FT/SEC/SEC":GOTO 162
153 ? "THEN YOU'RE ON EARTH.":? "ACCELERATION=32.16 FT/SEC/SEC":GOTO 163
154 ? "FINE. YOU'RE ON THE MOON.":? "ACCELERATION=5.15 FT/SEC/SEC":GOTO 164
155 ? "ALRIGHT. YOU'RE ON MARS.":? "ACCELERATION=12.5 FT/SEC/SEC":GOTO 165
156 ? "FINE. YOU'RE ON JUPITER.":? "ACCELERATION=85.2 FT/SEC/SEC":GOTO 166
157 ? "FINE. YOU'RE ON SATURN.":? "ACCELERATION=37.6 FT/SEC/SEC":GOTO 167
158 ? "ALRIGHT. YOU'RE ON URANUS.":? "ACCELERATION=33.8 FT/SEC/SEC":GOTO 168
159 ? "THEN YOU'RE ON NEPTUNE.":? "ACCELERATION=39.6 FT/SEC/SEC":GOTO 169
160 ? "FINE. YOU'RE ON THE SUN.":? "ACCELERATION=896 FT/SEC/SEC":GOTO 170
161 A2=12.2:GOTO 145
162 A2=28.3:GOTO 145
163 A2=32.16:GOTO 145
164 A2=5.15:GOTO 145
165 A2=12.5:GOTO 145
166 A2=85.2:GOTO 145
167 A2=37.6:GOTO 145
168 A2=33.8:GOTO 145
169 A2=39.6:GOTO 145
170 A2=896:GOTO 145
205 ? 
206 ? "ALTITUDE       ";D1;" FT"
207 ? "TERM. VELOCITY ";V1;" FT/SEC +-5%"
208 ? "ACCELERATION   ";A2;" FT/SEC/SEC +-5%"
210 ? "SET THE TIMER FOR YOUR FREEFALL."
211 ? "HOW MANY SECONDS";:INPUT T
215 ? "HERE WE GO."
217 ? 
218 ? "TIME (SEC)","DIST TO FALL (FT)"
219 ? "==========","================="
300 TVR=0:FOR I=0 TO T STEP (T/8)
310   IF I>V/A AND TVR=0 THEN TVR=1:GOTO 400
320   D=D1-((A/2)*I^2)
330   IF D<=0 THEN POP :GOTO 1000
340   ? I,,D
345   IF I<T THEN PAUSE INT((T/8)*60)
350 NEXT I
360 GOTO 500
400 ? "TERMINAL VELOCITY REACHED AT":? "T+";V/A;" SECONDS"
405 D=D1-((V^2/(2*A))+(V*(I-(V/A))))
420 IF D<=0 THEN 1010
430 ? I,,D
440 NEXT I
500 GOSUB 2180:? "CHUTE OPEN"
510 K=0:K1=0
530 FOR I=0 TO 99
550   IF A(I)=0 THEN 620
560   K=K+1
570   IF O>=A(I) THEN 600
580   K1=K1+1
600 NEXT I
620 A(I)=D
630 I=I-1
650 IF K-K1<=0.1*K THEN 700
660 IF K-K1<=0.25*K THEN 710
670 IF K-K1<=0.5*K THEN 720
680 IF K-K1<=0.75*K THEN 730
690 IF K-K1<=0.9*K THEN 740
695 GOTO 750
700 ? "WOW! THAT'S SOME JUMPING. OF THE"
701 ? K;" SUCCESSFUL JUMPS BEFORE YOURS,"
702 ? "ONLY ";K-K1;" OPENED THEIR CHUTES"
703 ? "LOWER THAN YOU DID.":GOTO 2000
710 ? "PRETTY GOOD! ";K;" SUCCESSFUL JUMPS"
711 ? "PRECEDED YOURS AND ONLY ";K-K1
712 ? "OF THEM GOT LOWER THAN YOU DID"
713 ? "BEFORE THEIR CHUTES OPENED.":GOTO 2000
720 ? "NOT BAD. THERE HAVE BEEN ";K:? " SUCCESSFUL JUMPS BEFORE YOURS."
721 ? "YOU WERE BEATEN OUT BY ";K-K1;" OF":? "THEM.":GOTO 2000
730 ? "CONSERVATIVE AREN'T YOU? YOU RANKED"
731 ? "ONLY ";K-K1;" IN THE ";K:? "SUCCESSFUL JUMPS BEFORE YOURS.":GOTO 2000
740 ? "HUMPH! DON'T YOU HAVE ANY SPORTING"
741 ? "BLOOD? THERE WERE ";K;" SUCCESSFUL"
742 ? "JUMPS BEFORE YOURS AND YOU CAME IN"
743 ? K1;" JUMPS BETTER THAN THE WORST.":? "SHAPE UP!!!":GOTO 2000
750 ? "HEY! YOU PULLED THE RIP CORD MUCH"
751 ? "TOO SOON. ";K;" SUCCESSFUL JUMPS"
752 ? "BEFORE YOURS AND YOU CAME IN NUMBER"
753 ? K-K1;"! GET WITH IT!":GOTO 2000
800 ? "REQUIESCAT IN PACE.":GOTO 1950
801 ? "MAY THE ANGEL IN HEAVEN LEAD YOU":? "INTO PARADISE":GOTO 1950
802 ? "REST IN PEACE":GOTO 1950
803 ? "SON-OF-A-GUN":GOTO 1950
804 ? "#$%&&%!$":GOTO 1950
805 ? "A KICK IN THE PANTS IS A BOOST IF":? "YOU'RE HEADED RIGHT":GOTO 1950
806 ? "HMMM. SHOULD HAVE PICKED A SHORTER":? "TIME.":GOTO 1950
807 ? "MUTTER. MUTTER. MUTTER.":GOTO 1950
808 ? "PUSHING UP DAISIES.":GOTO 1950
809 ? "EASY COME, EASY GO.":GOTO 1950
1000 ? SQR(2*O1/A),"SPLAT":GOSUB 2110
1005 ON INT(1+(10*RND(0))) GOTO 800,801,802,803,804,805,806,807,808,809
1010 ? (V/A)+((D1-(V^2/(2*A)))/V),"SPLAT":GOSUB 2110
1020 GOTO 1005
1950 ? "I'LL GIVE YOU ANOTHER CHANCE."
2000 ? "DO YOU WANT TO PLAY AGAIN";:INPUT Z$
2001 IF Z$="YES" THEN 118
2002 IF Z$="NO" THEN 2005
2003 ? "YES OR NO":GOTO 2000
2005 ? "PLEASE ";:INPUT Z$:IF Z$="YES" THEN 118
2007 ? "SSSSSSSS."
2046 TRAP 2060:? :? "SAVING PARACHUTE DROP DATA":OPEN #1,8,0,"D:PARACH.UTE"
2050 FOR I=0 TO 99:J=A(I):%PUT #1;J:NEXT I
2055 GOTO 2090
2060 CLOSE #1:? "UNABLE TO WRITE PARACHUTE FILE."
2070 ? "DO YOU WANT TO TRY ANOTHER DISK?":? "(YES OR NO)";:INPUT A1$
2080 IF A1$="YES" THEN ? "INSERT NEW DISK AND PRESS RETURN";:INPUT A1$:GOTO 2046
2090 CLOSE #1:TRAP 40000:END 
2100 REM Splat!
2110 COL=48:FOR I=44 TO 75 STEP 6
2120   SOUND 0,I,16,15:SETCOLOR 2,1,COL
2130   COL=COL+2:IF COL>56 THEN COL=48
2140   PAUSE 2:NEXT I
2150 SOUND 0,0,0,0:SETCOLOR 2,9,52
2160 RETURN 
2170 REM Parachute open
2180 FOR I=220 TO 123 STEP -8:SOUND 0,I,8,5:PAUSE 1:NEXT I:SOUND 0,0,0,0
2190 RETURN 
