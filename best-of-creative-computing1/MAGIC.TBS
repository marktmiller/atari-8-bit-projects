5 REM From Best of Creative Computing Vol. 1, p. 271
10 DIM I$(1)
20 ? "GAME OF MAGIC SQUARE BY DAVID AHL":? 
25 ? "PLAYERS ALTERNATELY CHOOSE AN INTEGER"
30 ? "(1 TO 9) THAT HAS NOT BEEN PREVIOUSLY"
35 ? "USED AND PLACE IT IN ANY UNFILLED"
40 ? "CELL OF A TIC-TAC-TOE BOARD. THE GOAL"
45 ? "IS TO MAKE THE SUM OF EACH ROW,":? "COLUMN, AND DIAGONAL EQUAL TO 15.":? 
50 ? "THAT PLAYER LOSES WHO FIRST MAKES"
55 ? "THE SUM OF THE THREE FIGURES IN ANY"
60 ? "ROW, COLUMN, OR DIAGONAL SOMETHING":? "OTHER THAN 15.":? 
62 ? "A TIE GAME DRAWS A MAGIC SQUARE!!":? 
63 INPUT "PRESS RETURN TO CONTINUE";I$
65 ? :? "THE COMPUTER WILL ASK YOU ON EACH"
70 ? "MOVE WHICH CELL YOU WISH TO OCCUPY"
75 ? "AND THE NUMBER YOU WISH TO PLACE IN"
80 ? "THAT CELL. YOUR INPUT SHOULD LOOK":? "LIKE '3,7' IF YOU WISHED TO PLACE A":? "7 IN CELL 3."
85 ? :? "HERE ARE THE CELL NUMBERS:":? 
90 ? "1 2 3":? "4 5 6":? "7 8 9"
92 DIM A(9),B(9)
95 FOR I=1 TO 9:A(I-1)=0:B(I-1)=0:NEXT I:M=0:W=0:GOSUB 960
100 ? :INPUT "YOUR MOVE -- CELL AND NUMBER";I,N
105 IF I<1 OR I>9 OR N<1 OR N>9 THEN 130
110 IF A(I-1)=0 AND B(N-1)=0 THEN 150
130 ? "ILLEGAL MOVE ... AGAIN,":GOTO 100
150 A(I-1)=N:B(N-1)=1:M=M+1:REM AN ACCEPTABLE MOVE
170 GOSUB 960:REM PRINT BOARD
180 GOSUB 800:REM A LOSING MOVE??
200 IF W=0 THEN 230:REM GOOD MOVE, NO WINNER YET
210 ? "SORRY, YOU LOSE -- NICE TRY.":GOTO 560
230 IF M<5 THEN 400:REM A TIE GAME?
240 ? "A TIE GAME -- BUTE WE'VE DRAWN A MAGIC SQUARE!":GOTO 560
400 FOR Q=1 TO 9:REM COMPUTER LOOKS FOR A GOOD MOVE
410   IF A(Q-1)>0 THEN 480:REM CELL TAKEN??
420   FOR R=1 TO 9
430     IF B(R-1)>0 THEN 470:REM NUMBER TAKEN??
435     A(Q-1)=R:REM TRY MAKING THE MOVE
440     GOSUB 800:REM IS THIS MOVE A LOSER?
450     IF W=0 THEN 500:REM A GOOD MOVE!!
460     Q1=Q:R1=R:W=0:A(Q-1)=0:REM RECORD BAD MOVE IN CASE NO GOOD ONE
470   NEXT R
480 NEXT Q
490 W=1:R=R1:Q=Q1:A(Q-1)=R:REM IF GET HERE, COMPUTER HAS LOST
500 B(R-1)=1:REM COMPUTER MAKES ITS MOVE
520 ? "I MOVE TO CELL ";Q;" WITH A ";R
530 GOSUB 960:REM PRINT BOARD
540 IF W=0 THEN 100:REM THE GAME GOES ON....
550 ? "I LOSE -- YOU WIN!!"
560 ? :FOR I=1 TO 15:? CHR$(253);:NEXT I:REM RINGS BELL
570 ? :? "LET'S PLAY AGAIN...":GOTO 95
800 FOR X=1 TO 8:REM SUBROUTINE TO CHECK FOR LOSING MOVE
810   ON X GOTO 820,830,840,850,860,870,880,890
820   J=1:K=2:L=3:GOTO 900
830   K=4:L=7:GOTO 900
840   K=5:L=9:GOTO 900
850   J=4:L=6:GOTO 900
860   J=2:L=8:GOTO 900
870   J=3:L=7:GOTO 900
880   K=6:L=9:GOTO 900
890   J=7:K=8
900   IF A(J-1)=0 OR A(K-1)=0 OR A(L-1)=0 THEN 930
920   IF A(J-1)+A(K-1)+A(L-1)<>15 THEN 940
930 NEXT X
935 GOTO 950
940 W=1:REM A LOSER!!
950 RETURN 
960 ? :? A(0);A(1);A(2):REM SUBROUTINE TO PRINT BOARD
970 ? A(3);A(4);A(5):? A(6);A(7);A(8):? :RETURN 
999 END 
