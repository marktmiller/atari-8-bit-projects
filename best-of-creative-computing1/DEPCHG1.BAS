3 REM From Best of Creative Computing, Vol. 1, p. 251
5 REM Original by Dana Noftle
7 REM Version 1
10 PRINT "DEPTH CHARGE GAME":PRINT 
20 PRINT "DIMENSION OF SEARCH AREA";:INPUT G:PRINT 
30 N=INT((LOG(G)/LOG(2)))+1
35 DIM A$(1)
40 PRINT "YOU ARE CAPTAIN OF THE DESTROYER USS"
50 PRINT "DIGITAL. AN ENEMY SUB HAS BEEN"
60 PRINT "CAUSING YOU TROUBLE. YOUR MISSION"
70 PRINT "IS TO DESTROY IT. YOU HAVE ";N;" SHOTS."
80 PRINT "SPECIFY DEPTH CHARGE EXPLOSION POINT"
90 PRINT "WITH A TRIO OF NUMBERS -- THE FIRST":PRINT "TWO ARE THE SURFACE COORDINATES. THE"
95 PRINT "THIRD IS THE DEPTH."
100 PRINT :PRINT "GOOD LUCK!":PRINT :SHIPHIT=0
110 A=INT(G*RND(1)):B=INT(G*RND(1)):C=INT(G*RND(1))
120 FOR D=1 TO N:PRINT :PRINT "TRIAL #";D;:INPUT X,Y,Z
130   IF ABS(X-A)+ABS(Y-B)+ABS(Z-C)=0 THEN 300
140   GOSUB 500:PRINT :NEXT D
200 SHIPHIT=1:GOSUB 800:PRINT :PRINT "YOU HAVE BEEN TORPEDOED! ABANDON SHIP!"
210 PRINT "THE SUBMARINE WAS AT ";A;",";B;",";C:GOTO 400
300 PRINT :PRINT "B O O M ! ! YOU FOUND IT IN ";D;" TRIES!":SHIPHIT=0:GOSUB 860
400 PRINT :PRINT :PRINT "ANOTHER GAME (Y OR N)";:INPUT A$
410 IF A$="Y" THEN 100
420 PRINT "OK. HOPE YOU ENJOYED YOURSELF.":GOTO 1100
500 GOSUB 1000:PRINT "SONAR REPORTS SHOT WAS ";
510 IF Y>B THEN PRINT "NORTH";
520 IF Y<B THEN PRINT "SOUTH";
530 IF X>A THEN PRINT "EAST";
540 IF X<A THEN PRINT "WEST";
550 IF Y<>B OR X<>A THEN PRINT :PRINT "AND";
560 IF Z>C THEN PRINT " TOO LOW "
570 IF Z<C THEN PRINT " TOO HIGH "
580 IF Z=C THEN PRINT " DEPTH OK "
590 RETURN 
795 REM A TORPEDO APPROACHES!
800 V=97
810 FOR TORP=30 TO 5 STEP -1
820   SOUND 0,TORP,50,V
825   SOUND 1,1,94,V
830   FOR DELAY=1 TO 10:NEXT DELAY
840   IF TORP=25 OR TORP=20 OR TORP=10 OR TORP=5 OR TORP=1 THEN V=V+1
850 NEXT TORP
853 SOUND 1,0,0,0
855 REM BOOM!
860 COL=48
865 EXPLOSION=255:IF SHIPHIT=1 THEN EXPLOSION=100
870 FOR V=110 TO 96 STEP -1
880   SOUND 0,EXPLOSION,30,V
890   IF SHIPHIT=1 THEN SETCOLOR 2,0,COL
900   FOR DELAY=1 TO 30:NEXT DELAY
910   COL=COL+2:IF COL>56 THEN COL=48
920 NEXT V
930 SOUND 0,0,0,0:SETCOLOR 2,6,52:RETURN 
1000 REM SONAR
1010 SOUND 1,29,100,97:SOUND 2,25,98,99
1020 FOR V=110 TO 96 STEP -1
1030   SOUND 0,29,100,V
1040   FOR DELAY=1 TO 10:NEXT DELAY
1050 NEXT V
1060 SOUND 0,0,0,0
1070 FOR DELAY=1 TO 100:NEXT DELAY
1080 SOUND 1,0,0,0:SOUND 2,0,0,0:RETURN 
1100 END 
