3 REM From Best of Creative Computing, Vol. 1, p. 251
5 REM Original by Dan Noftle
7 REM Version 2 enhanced
10 PRINT "DEPTH CHARGE GAME":PRINT 
15 GOSUB 1170
20 PRINT "DIMENSION OF SEARCH AREA";:INPUT G:PRINT 
30 PRINT "ENTER THE NUMBER OF SHOTS YOU WANT ";:INPUT N
35 DIM A$(1)
40 PRINT "YOU ARE CAPTAIN OF THE DESTROYER USS"
50 PRINT "DIGITAL. AN ENEMY SUB HAS BEEN"
60 PRINT "CAUSING YOU TROUBLE. YOUR MISSION"
70 PRINT "IS TO DESTROY IT. YOU HAVE ";N;" SHOTS."
80 PRINT "SPECIFY DEPTH CHARGE EXPLOSION POINT"
90 PRINT "WITH A TRIO OF NUMBERS -- THE FIRST":PRINT "TWO ARE THE SURFACE COORDINATES. THE"
95 PRINT "THIRD IS THE DEPTH."
100 PRINT :PRINT "GOOD LUCK!":PRINT :SHIPHIT=0
110 A=INT(G*RND(1)):B=INT(G*RND(1)):C=INT(G*RND(1))
120 FOR D=1 TO N:PRINT :PRINT "TRIAL #";D;:INPUT X,Y,Z
130   IF ABS(X-A)+ABS(Y-B)+ABS(Z-C)=0 THEN GOSUB 1030:FOR DELAY=1 TO Z*100:NEXT DELAY:GOTO 300
140   GOSUB 430:PRINT :NEXT D
200 SHIPHIT=1:GOSUB 810:PRINT :PRINT "YOU HAVE BEEN TORPEDOED! ABANDON SHIP!"
210 PRINT "THE SUBMARINE WAS AT ";A;",";B;",";C:GOTO 400
300 PRINT :PRINT "B O O M ! ! YOU FOUND IT IN ";D;" TRIES!":SHIPHIT=0:GOSUB 910
400 PRINT :PRINT :PRINT "ANOTHER GAME (Y OR N)";:INPUT A$
410 IF A$="Y" THEN 100
420 PRINT "OK. HOPE YOU ENJOYED YOURSELF.":END 
425 REM Shot misses
430 GOSUB 1030
440 FOR DELAY=1 TO Z*100:NEXT DELAY
450 GOSUB 910
500 PRINT "SONAR REPORTS SHOT WAS ";
510 IF Y>B THEN PRINT "NORTH";
520 IF Y<B THEN PRINT "SOUTH";
530 IF X>A THEN PRINT "EAST";
540 IF X<A THEN PRINT "WEST";
550 IF Y<>B OR X<>A THEN PRINT :PRINT "AND";
560 IF Z>C THEN PRINT " TOO LOW "
570 IF Z<C THEN PRINT " TOO HIGH "
580 IF Z=C THEN PRINT " DEPTH OK "
590 REM VARIANT-MOVE SHIP ONE SQUARE IN A RANDOM DIRECTION AFTER EACH TRIAL SHOT
600 NDIR=INT(6*RND(1))
610 IF NDIR=1 THEN A=A+1
620 IF NDIR=2 THEN A=A-1
630 IF A<0 THEN A=0:GOTO 600
640 IF A>G-1 THEN A=G-1:GOTO 600
650 IF NDIR=3 THEN B=B+1
660 IF NDIR=4 THEN B=B-1
670 IF B<0 THEN B=0:GOTO 600
680 IF B>G-1 THEN B=G-1:GOTO 600
690 IF NDIR=5 THEN C=C+1
700 IF NDIR=6 THEN C=C-1
710 IF C<0 THEN C=0:GOTO 600
720 IF C>G-1 THEN C=G-1:GOTO 600
730 RETURN 
800 REM A TORPEDO APPROACHES!
810 V=97
820 FOR TORP=30 TO 5 STEP -1
830   SOUND 0,TORP,50,V
840   SOUND 1,1,94,V
850   FOR DELAY=1 TO 10:NEXT DELAY
860   IF TORP=25 OR TORP=20 OR TORP=10 OR TORP=5 OR TORP=1 THEN V=V+1
870 NEXT TORP
880 SOUND 1,0,0,0
890 REM BOOM!
900 COL=48
910 EXPLOSION=255:IF SHIPHIT=1 THEN EXPLOSION=100:DVH=0:DVL=0:GOTO 950
920 VL=Z:REM Quiet explosion at lower depth
940 IF VL>6 THEN VL=6
950 FOR V=15-VL TO 0 STEP -1
960   SOUND 0,EXPLOSION,0,V
970   IF SHIPHIT=1 THEN SETCOLOR 2,0,COL:GOSUB 1210:GOTO 990
980   FOR DELAY=1 TO 30:NEXT DELAY
990   COL=COL+2:IF COL>56 THEN COL=48
1000 NEXT V
1010 SOUND 0,0,0,0:SETCOLOR 2,6,52:RETURN 
1020 REM Launch depth charge sound
1030 FOR V=110 TO 96 STEP -1
1040   SOUND 0,30,50,V
1050   FOR DELAY=1 TO 10:NEXT DELAY
1060 NEXT V
1070 SOUND 0,0,0,0:FOR DELAY=1 TO 50:NEXT DELAY
1080 REM Splash
1090 FOR V=110 TO 96 STEP -1
1100   SOUND 0,1,50,V
1110   FOR DELAY=1 TO 15:NEXT DELAY
1120 NEXT V
1130 SOUND 0,0,0,0
1140 RETURN 
1150 END 
1160 REM Get location of display list
1170 DLST=PEEK(560)+PEEK(561)*256
1180 SCADR=PEEK(DLST+4)
1190 RETURN 
1200 REM Shake!
1210 POKE DLST+4,SCADR-42
1220 FOR DELAY=1 TO 5:NEXT DELAY
1230 POKE DLST+4,SCADR
1240 FOR DELAY=1 TO 5:NEXT DELAY
1250 POKE DLST+4,SCADR+42
1260 FOR DELAY=1 TO 5:NEXT DELAY
1270 POKE DLST+4,SCADR
1280 FOR DELAY=1 TO 5:NEXT DELAY
1290 RETURN 
