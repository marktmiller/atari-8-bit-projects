10 IP=%0:SP=-1:TR=%1
20 DSIZE=%1:CODELEN=8:STACKLEN=10:OPCODES=17
30 IF DSIZE>%0 THEN DIM GLOBALS(DSIZE)
40 DIM CODE(CODELEN),STACK(STACKLEN),OPANDCNT(OPCODES),OPCODE$(6),INST$(OPCODES*6+6)
50 FOR I=%0 TO CODELEN-%1:CODE(I)=%0:NEXT I:FOR I=%0 TO STACKLEN-%1:STACK(I)=%0:NEXT I
60 RESTORE #PRG:FOR I=%0 TO CODELEN-%1:READ X:CODE(I)=X:NEXT I
70 RESTORE #OPCODES:FOR B=%0 TO OPCODES-%1:READ OPCODE$,ARGCNT:INST$(B*6+%1)=OPCODE$:OPANDCNT(B)=ARGCNT:NEXT B
80 EXEC EXCODE
90 END 
100 PROC EXCODE
110   WHILE IP<CODELEN AND CODE(IP)<>16
120     OPCODE=CODE(IP)
130     IF TR=%1 THEN EXEC DISASSEMBLE
140     IP=IP+%1
150     IF OPCODE=14:? STACK(SP);:SP=SP-%1:GO# TRACING:ENDIF 
160     IF OPCODE=9:SP=SP+%1:STACK(SP)=CODE(IP):IP=IP+%1:GO# TRACING:ENDIF 
170     IF OPCODE=13:GLOBALS(CODE(IP))=STACK(SP):SP=SP-%1:IP=IP+%1:GO# TRACING:ENDIF 
180     IF OPCODE=11:SP=SP+%1:STACK(SP)=GLOBALS(CODE(IP)):IP=IP+%1:ENDIF 
190     # TRACING:IF TR=1 THEN EXEC DISPSTACK
200   WEND 
210   IF TR=1:EXEC DISASSEMBLE:EXEC DISPSTACK:EXEC DUMPDATAMEM:ENDIF 
220 ENDPROC 
780 PROC DISASSEMBLE
790   OPANDS=OPANDCNT(OPCODE)
800   IF IP>=CODELEN THEN GO# ENDDISASSEM
810   ? IP;:? ": ";:? INST$(OPCODE*6+%1,OPCODE*6+6);
820   IF OPANDS=%1:? " ";:? CODE(IP+%1);:ENDIF 
830   # ENDDISASSEM:ENDPROC 
860 PROC DISPSTACK
870   ? "  STACK=[";
880   IF SP>=0:FOR K=%0 TO SP:? " ";:? STACK(K);:NEXT K:ENDIF 
910   ? " ]"
920 ENDPROC 
930 PROC DUMPDATAMEM
940   IF DSIZE=%0 THEN GO# ENDDUMP
950   ? :? "DATA MEMORY:":FOR I=%0 TO DSIZE-%1:? I;:? ": ";:? GLOBALS(I):NEXT I
960   # ENDDUMP:ENDPROC 
1000 # PRG:DATA 9,99,13,0,11,0,14,16
2000 # OPCODES:DATA NULL  ,0,IADD  ,0,ISUB  ,0,IMUL  ,0,ILT   ,0,IEQ   ,0,BR    ,1,BRT   ,1,BRF   ,1,ICONST,1
2010 DATA LOAD  ,1,GLOAD ,1,STORE ,1,GSTORE,1,PRINT ,0,POP   ,0,HALT  ,0
